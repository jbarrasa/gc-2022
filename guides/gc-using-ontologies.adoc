= GC Workshop - Three ways you can use ontologies with Neo4j

== Example 1: Interoperability

== Exploring an ontology

There are several ways to explore an RDF-based ontology using n10s

Parse the triples in the onto...

[source,cypher]
----
call n10s.rdf.stream.fetch("https://schema.org/version/latest/schemaorg-current-https.nt","N-Triples")
----

List the classes (categories) defined in the onto...

[source,cypher]
----
call n10s.rdf.stream.fetch("https://schema.org/version/latest/schemaorg-current-https.nt","N-Triples", { limit : 99999 }) yield subject, predicate, object
where predicate = "http://www.w3.org/1999/02/22-rdf-syntax-ns#type" and
	  object = "http://www.w3.org/2000/01/rdf-schema#Class"
return subject
----

List the elements -both properties and relationships- defined in the onto for a given category (Organization)...

[source,cypher]
----
call n10s.rdf.stream.fetch("https://schema.org/version/latest/schemaorg-current-https.nt","N-Triples", { limit : 99999 }) yield subject, predicate, object
where predicate = "https://schema.org/domainIncludes" and
	  object = "https://schema.org/Organization"
with collect (subject) as orgProps
call n10s.rdf.stream.fetch("https://schema.org/version/latest/schemaorg-current-https.nt","N-Triples", { limit : 99999 }) yield subject, predicate, object
where subject in orgProps and predicate = "http://www.w3.org/2000/01/rdf-schema#comment"
return subject, object
----


== Load data into Neo4j using an ontology as the target model

A quick look at a simpler ontology...

[source,cypher]
----
call n10s.rdf.stream.fetch("https://raw.githubusercontent.com/jbarrasa/gc-2022/main/interop/onto/rail-simple.ttl","Turtle")
----

Automatically generating a data importer model from the ontology (experimental):

[source,cypher]
----
call n10s.experimental.stream.dimodel.fetch("https://raw.githubusercontent.com/jbarrasa/gc-2022/main/interop/onto/rail-simple.ttl","Turtle")
----

Open the model in https://data-importer.graphapp.io/[data importer] and carry out import on your neo4j instance from the https://github.com/jbarrasa/gc-2022/tree/main/interop/data[data files].

Then if you want an instant (ootb) RDF endpoint, add the mappings to the ontology.

[source,cypher]
----
CALL n10s.nsprefixes.add(...);
...

CALL n10s.mapping.add(...);
----


== We can test the RDF endpoint

The endpoint exposes the graph data as

by node id... (pick any from your graph)

[source,cypher]
----
:get /rdf/neo4j/describe/...
----

or using a cypher query...

All stations adjacent to London Bridge station (LBG)

[source,cypher]
----
MATCH (:Station { stationCode: 'LBG'})-[:link]-(s) RETURN  s
----

[source,cypher]
----
:post /rdf/neo4j/cypher
{
"cypher":"MATCH (:Station { stationCode: 'LBG'})-[:link]-(s) RETURN  s",
"format":"N-Triples"
}
----

What about properties in relationships? Use `Turtle-star` format

== (optional) Add geo point for presentation purposes and build a dashboard?

[source,cypher]
----
match (s:Station) set s.point = point({latitude: s.lat, longitude: s.long})
----

Path between two stations

[source,cypher]
----
match p = shortestPath((:Station { stationCode: "CTR"})-[:link*..40]->(:Station { stationCode: "HPD"}))
return p
----

Try Neodash from the GraphApp Gallery with https://github.com/jbarrasa/gc-2022/blob/main/interop/other/dashboard.json[this predefined dashboard].


== Example 2: Semantic Search

First we'll build the KG in two steps: first we load the data and then we overlay the ontology.

Once the KG is built, we'll run a few semantic searches on it.

== Configure the DB so that it's ready to import RDF data

We first create the required constraint

[source,cypher]
----
CREATE CONSTRAINT n10s_unique_uri ON (r:Resource) ASSERT r.uri IS UNIQUE;
----

And set the config for importing ontologies:

[source,cypher]
----
call n10s.graphconfig.init({ handleVocabUris: "IGNORE", classLabel: "Concept", subClassOfRel: "broader"});
----

== Loading the data: Articles with pre-extracted entities

Use the https://data-importer.graphapp.io/[data importer] with https://github.com/jbarrasa/gc-2022/blob/main/search/other/imodel.json[this model]
and https://github.com/jbarrasa/gc-2022/tree/main/search/data[this set of data files].


== Ontology Import

Let's import the concept scheme

[source,cypher]
----
call n10s.skos.import.fetch("https://raw.githubusercontent.com/jbarrasa/goingmeta/main/session2/resources/goingmeta-skos.ttl","Turtle");
----

Let's 'clean' the taxonomy: remove redundant `broader` relationships (from Wikidata extract).

[source,cypher]
----
match (s:Concept)-[shortcut:broader]->(:Concept)<-[:broader*2..]-(s)
delete shortcut;
----

== Some Semantic searches on the KG (1)

__Articles on "NoSQL database management system"__

This will require returning articles both directly or indirectly connected to that category.

[source,cypher]
----
MATCH path = (:Concept {prefLabel: "NoSQL database management system"})<-[:broader*0..]-(sc)<-[:refers_to]-(art:Article)
return art.title, [x in nodes(path) where x:Concept | coalesce(x.prefLabel,"") + coalesce(x.label,"") ]
----

or using the inferencing method in n10s

[source,cypher]
----
match (c:Concept {prefLabel: "NoSQL database management system"})
call n10s.inference.nodesInCategory(c, { inCatRel: "refers_to"}) yield node as article
return article.title as result
----

== Some Semantic searches on the KG (2)

__Read next__ type of queries for recommendation/personalisation.

Use this query to create a search phrase in Bloom and visually explore recommendation paths.

[source,cypher]
----
match simpath = (a:Article)-[:refers_to]->(cat)-[:broader*0..1]->()<-[:broader*0..1]-()<-[:refers_to]-(other)
where a.uri = "https://dev.to/qainsights/performance-testing-neo4j-database-using-bolt-protocol-in-apache-jmeter-1oa9"
return other.title, [x in nodes(simpath) where x:Concept | coalesce(x.prefLabel,x.label) ] as explain
----

The quality of the results can be improved by adding another ontology (software stack onto)

[source,cypher]
----
CALL n10s.onto.import.fetch("http://www.nsmntx.org/2020/08/swStacks","Turtle");
----


== Example 3: Model Validation

== Loading the data: The Northwind Graph

Use the https://data-importer.graphapp.io/[data importer] with https://github.com/jbarrasa/gc-2022/blob/main/validation/other/imodel.json[this model]
and https://github.com/jbarrasa/gc-2022/tree/main/validation/data[this set of data files].

== Load a subset of the SHACL shapes

[source,cypher]
----
call n10s.validation.shacl.import.inline('


@prefix ex: <http://example.neo4j.com/graphvalidation#> .
@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix neo4j: <neo4j://graph.schema#> .
@prefix xsd: <http://www.w3.org/2001/XMLSchema#> .

ex:SupplierShape a sh:NodeShape ;
  sh:targetClass neo4j:Supplier ;
  sh:property [
    sh:path neo4j:companyName ;
    sh:pattern "^\\w[^&]*$" ;
    sh:maxCount 1 ;
    sh:datatype xsd:string ;
  ] .

','Turtle')
----

And validate the graph...

[source,cypher]
----
call n10s.validation.shacl.validate()
----

== Data changes over time...

Let's modify the graph adding a second company name for a random supplier:

[source,cypher]
----
match (s:Supplier) with s limit 1
set s.companyName = [s.companyName, replace(s.companyName, " ", "")]
return s
----

And let's re-run the Validation

[source,cypher]
----
call n10s.validation.shacl.validate()
----

== A richer set of constraints

They can be loaded inline like before or retrieved directly from a file or a service.

[source,cypher]
----
call n10s.validation.shacl.import.fetch("https://raw.githubusercontent.com/jbarrasa/gc-2022/main/validation/shapes/northwind-shacl.ttl","Turtle")
----

And we can list the constraints that apply to a given category of nodes (label).

[source,cypher]
----
call n10s.validation.shacl.listShapes() yield target, propertyOrRelationshipPath, param, value
where target = 'Supplier'
return target, propertyOrRelationshipPath, param, value
----

We can re-run the validation once more...

[source,cypher]
----
call n10s.validation.shacl.validate()
----

And introduce more violations

[source,cypher]
----
match (p:Product) with p limit 1 set p.unitPrice = toString(p.unitPrice) return p
----

And filter the results by node

[source,cypher]
----
call n10s.validation.shacl.validate() yield focusNode, nodeType, shapeId, propertyShape, offendingValue, resultPath, severity, resultMessage
where focusNode = $nodeid
return focusNode, nodeType, shapeId, propertyShape, offendingValue, resultPath, severity, resultMessage
----

== Using the severity to define business level validations

Move from low level data quality to business level checks

[source,cypher]
----
call n10s.validation.shacl.import.fetch("https://raw.githubusercontent.com/jbarrasa/gc-2022/main/validation/shapes/northwind-custom-sev-shacl.ttl","Turtle")
----


== Using a dashboard to visualise validation results

Try Neodash with https://github.com/jbarrasa/gc-2022/blob/main/validation/other/dashboard.json[this predefined dashboard].
